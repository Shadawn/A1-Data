
// Механизм предназначен для эмуляции регламентных заданий в файловой базе, где они выполняются нестабильно.
// Каждый пользователь каждые 5 минут опрашивает БД на предмет, когда был прошлый запуск.
// Если он был более чем 5 минут ранее, то в фоновом задании запускается обработчик А1Э_РегламентноеЗаданиеКаждыеПятьМинут.
// 
// Возвращаемое значение:
//   - 
//
Функция НастройкиМеханизма() Экспорт
	Настройки = А1Э_Механизмы.НовыйНастройкиМеханизма();
	
	Настройки.Обработчики.Вставить("А1Э_РегулярныеПроцессы_КаждыеПятьМинутНаСервере", Истина);
	
	Возврат Настройки;
КонецФункции

#Если НЕ Клиент Тогда
	
	Функция А1Э_РегулярныеПроцессы_КаждыеПятьМинутНаСервере(ДанныеСервера) Экспорт
		УстановитьПривилегированныйРежим(Истина);
		Если А1Обновление_Расширения.Устарели() Тогда Возврат Неопределено; КонецЕсли;
		
		ДатаПрошлогоЗапуска = ДатаПрошлогоЗапуска(); 
		Если ТекущаяДата() - ДатаПрошлогоЗапуска > 300 Тогда
			ДатаПрошлогоЗапуска_Установить(ТекущаяДата());
			ФоновыеЗадания.Выполнить("А1Д_КаждыеПятьМинутФайловаяБаза.Обработчик");
		КонецЕсли;
	КонецФункции
	
	Функция Обработчик() Экспорт 
		УстановитьПривилегированныйРежим(Истина);
		А1Э_Механизмы.ВыполнитьМеханизмыОбработчика("А1Э_РегламентноеЗаданиеКаждыеПятьМинут");
	КонецФункции
	
	Функция ДатаПрошлогоЗапуска()
		НаборЗаписей = РегистрыСведений.А1Д_КаждыеПятьМинутФайловаяБаза_ДатаЗапуска.СоздатьНаборЗаписей();
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() = 0 Тогда
			Возврат Дата(1,1,1); 
		Иначе
			Возврат НаборЗаписей[0].Дата;
		КонецЕсли;
	КонецФункции
	
	Функция ДатаПрошлогоЗапуска_Установить(Дата) 
		Менеджер = РегистрыСведений.А1Д_КаждыеПятьМинутФайловаяБаза_ДатаЗапуска.СоздатьМенеджерЗаписи();
		Менеджер.Дата = Дата;
		Менеджер.Записать(Истина);
	КонецФункции 
	
#КонецЕсли
 